cmake_minimum_required (VERSION 3.15)
project(stb_image C CXX)

set(STB_IMAGE_LIBARY stb)
add_library(
	${STB_IMAGE_LIBARY}
        STATIC 
            stb/stb_image.h
            src/stb_image.cpp
)

target_include_directories(${STB_IMAGE_LIBARY} PRIVATE stb)
add_library(stb::stb ALIAS ${STB_IMAGE_LIBARY})

set_target_properties(
	${STB_IMAGE_LIBARY} PROPERTIES
	CXX_STANDARD 20
	CXX_STANDARD_REQUIRED ON
	CXX_EXTENSIONS OFF
	C_STANDARD 17
	C_STANDARD_REQUIRED ON
	C_EXTENSIONS OFF
)

include(GNUInstallDirs)

target_include_directories(
	${STB_IMAGE_LIBARY} PUBLIC
	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
	$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

install(
	TARGETS ${STB_IMAGE_LIBARY}
	EXPORT "${STB_IMAGE_LIBARY}Targets"
	LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
	ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
	RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
	INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR} 
)

install(
	FILES stb/stb_image.h
	DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${STB_IMAGE_LIBARY}
)

install(
	EXPORT "${STB_IMAGE_LIBARY}Targets"
	FILE "${STB_IMAGE_LIBARY}Targets.cmake"
	NAMESPACE stb::
	DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${STB_IMAGE_LIBARY}
)

include(CMakePackageConfigHelpers)
configure_package_config_file(
	"${CMAKE_CURRENT_LIST_DIR}/config/${STB_IMAGE_LIBARY}Config.cmake.in"
	"${CMAKE_CURRENT_BINARY_DIR}/${STB_IMAGE_LIBARY}Config.cmake"
	INSTALL_DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/${STB_IMAGE_LIBARY}"
)

configure_package_config_file(
	"${CMAKE_CURRENT_LIST_DIR}/config/${STB_IMAGE_LIBARY}.pc.in"
	"${CMAKE_CURRENT_BINARY_DIR}/${STB_IMAGE_LIBARY}.pc"
	INSTALL_DESTINATION "${CMAKE_INSTALL_LIBDIR}/pkgconfig"
)

install(
	FILES "${CMAKE_CURRENT_BINARY_DIR}/${STB_IMAGE_LIBARY}Config.cmake"
	DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/${STB_IMAGE_LIBARY}"
)

install(
	FILES "${CMAKE_CURRENT_BINARY_DIR}/${STB_IMAGE_LIBARY}.pc"
	DESTINATION "${CMAKE_INSTALL_LIBDIR}/pkgconfig"
)